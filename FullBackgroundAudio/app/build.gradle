apply plugin: 'com.android.application'
// INFO: insert your license here or set JWPLAYER_LICENSE_KEY as environment variable
def licenseKey = System.getenv('JWPLAYER_LICENSE_KEY')
// Detect if build is on Jenkins
def isJenkinsBuild = System.getenv("BUILD_NUMBER") != null
def props = new Properties()
if (isJenkinsBuild) {
    def propsFile = rootProject.file("/etc/android_sdk/keystore.properties")
    props.load(new FileInputStream(propsFile))
}
android {
    compileSdk 33
    defaultConfig {
        applicationId "com.jwplayer.demo.fullbackgroundaudio"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
		buildConfigField "String", "JWPLAYER_LICENSE_KEY", """\"${licenseKey}\""""
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        release {
            // Only attempt to read keystore.properties for Jenkins builds
            if (isJenkinsBuild) {
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (isJenkinsBuild) {
                signingConfig signingConfigs.release
            }
        }
    }
    namespace 'com.jwplayer.demo.fullbackgroundaudio'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.media:media:1.6.0'
    
    def sdkVersion = '4.18.0'
	implementation "com.jwplayer:jwplayer-core:${sdkVersion}"
	implementation "com.jwplayer:jwplayer-common:${sdkVersion}"
}