apply plugin: 'com.android.application'
// INFO: insert your license here or set JWPLAYER_LICENSE_KEY as environment variable
def licenseKey = System.getenv('JWPLAYER_LICENSE_KEY')
// Detect if build is on Jenkins
def isJenkinsBuild = System.getenv("BUILD_NUMBER") != null
def props = new Properties()
if (isJenkinsBuild) {
    def propsFile = rootProject.file("/etc/android_sdk/keystore.properties")
    props.load(new FileInputStream(propsFile))
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.jwplayer.demo.nativecontrols"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		buildConfigField "String", "JWPLAYER_LICENSE_KEY", """\"${licenseKey}\""""
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        release {
            // Only attempt to read keystore.properties for Jenkins builds
            if (isJenkinsBuild) {
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isJenkinsBuild) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'


    def sdkVersion = '4.5.1'
	implementation "com.jwplayer:jwplayer-core:${sdkVersion}"
	implementation "com.jwplayer:jwplayer-common:${sdkVersion}"

    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.multidex:multidex:2.0.1'
}