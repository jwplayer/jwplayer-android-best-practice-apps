apply plugin: 'com.android.application'
// INFO: insert your license here or set JWPLAYER_LICENSE_KEY as environment variable
def licenseKey = System.getenv('JWPLAYER_LICENSE_KEY')
// Detect if build is on Jenkins
def isJenkinsBuild = System.getenv("BUILD_NUMBER") != null
def props = new Properties()
if (isJenkinsBuild) {
    def propsFile = rootProject.file("/etc/android_sdk/keystore.properties")
    props.load(new FileInputStream(propsFile))
}
android {
    compileSdk 34

    defaultConfig {
        applicationId "com.jwplayer.demo.recyclerview"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
		buildConfigField "String", "JWPLAYER_LICENSE_KEY", """\"${licenseKey}\""""
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        release {
            // Only attempt to read keystore.properties for Jenkins builds
            if (isJenkinsBuild) {
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isJenkinsBuild) {
                signingConfig signingConfigs.release
            }
        }
    }
    namespace 'com.jwplayer.demo.recyclerview'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'

    implementation'androidx.appcompat:appcompat:1.6.1'
    implementation'com.google.android.material:material:1.9.0'
    implementation 'androidx.media:media:1.6.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '2.1.4'
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.3.1'

    implementation group: 'com.google.android.gms', name: 'play-services-auth', version: '20.7.0'

    def sdkVersion = '4.18.4'
    implementation "com.jwplayer:jwplayer-core:${sdkVersion}"
    implementation "com.jwplayer:jwplayer-common:${sdkVersion}"

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
}
