apply plugin: 'com.android.application'
// INFO: insert your license here or set JWPLAYER_LICENSE_KEY as environment variable
def licenseKey = System.getenv('JWPLAYER_LICENSE_KEY')
// Detect if build is on Jenkins
def isJenkinsBuild = System.getenv("BUILD_NUMBER") != null
def props = new Properties()
if (isJenkinsBuild) {
    def propsFile = rootProject.file("/etc/android_sdk/keystore.properties")
    props.load(new FileInputStream(propsFile))
}
android {
    compileSdkVersion 30

    defaultConfig {

        minSdkVersion 21
        targetSdkVersion 30

        versionCode 1
        versionName "1.0"

        applicationId "com.jwplayer.chromecastjwplayerfragmentdemo"
		buildConfigField "String", "JWPLAYER_LICENSE_KEY", """\"${licenseKey}\""""
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        release {
            // Only attempt to read keystore.properties for Jenkins builds
            if (isJenkinsBuild) {
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isJenkinsBuild) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation'androidx.appcompat:appcompat:1.2.0'
    implementation'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    def sdkVersion = '4.0.0'
	implementation "com.jwplayer:jwplayer-core:${sdkVersion}"
	implementation "com.jwplayer:jwplayer-common:${sdkVersion}"
	implementation "com.jwplayer:jwplayer-chromecast:${sdkVersion}"
}
